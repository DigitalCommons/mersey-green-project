PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX essglobal: <https://lod.coop/essglobal/V2a/vocab/>
PREFIX gr: <http://purl.org/goodrelations/v1#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX osspatialrelations: <http://data.ordnancesurvey.co.uk/ontology/spatialrelations/>
PREFIX : <https://lod.coop/sea-lod/mersey-green/>

SELECT  ?uri ?name ?within ?lat ?lng
        ?manLat ?manLng
        ?desc ?www ?street ?locality ?postcode
        ?tel ?email ?twitter ?facebook
        ?primaryActivity ?secondaryActivities ?orgStructure
WHERE {
        ?uri rdf:type essglobal:SSEInitiative .
        ?uri gr:name ?name .

        OPTIONAL { ?uri essglobal:hasAddress ?addr .
	  # Only link to these if ?addr exists
	  
	  OPTIONAL {  ?addr osspatialrelations:within ?within .
	    # Only link to these if ?within exists
	    OPTIONAL { ?within geo:lat ?lat. }
	    OPTIONAL { ?within geo:long ?lng. }
	  }
	  
	  OPTIONAL { ?addr geo:lat ?manLat . }
          OPTIONAL { ?addr geo:long ?manLng . }
	  OPTIONAL { ?addr vcard:street-address ?street . }
	  OPTIONAL { ?addr vcard:postal-code ?postcode . }
	  OPTIONAL { ?addr vcard:locality ?locality . }
	}

        OPTIONAL { ?uri dc:description ?desc . }
        OPTIONAL { ?uri foaf:homepage ?www . }

        OPTIONAL { ?uri vcard:hasTelephone ?hasTel . }
        OPTIONAL { ?hasTel vcard:value ?tel . }
        OPTIONAL { ?uri vcard:hasEmail ?hasEmail . }
        OPTIONAL { ?hasEmail vcard:value ?email . }

        OPTIONAL {
            ?uri foaf:account ?accountf .
            ?accountf foaf:accountServiceHomepage "https://facebook.com/" .
            ?accountf foaf:accountName ?facebook .
        }
        OPTIONAL {
            ?uri foaf:account ?account .
            ?account foaf:accountServiceHomepage "https://twitter.com/" .
            ?account foaf:accountName ?twitter .
        }

        OPTIONAL { ?uri essglobal:primarySector ?primaryActivity . }
        OPTIONAL { ?uri essglobal:organisationalStructure ?orgStructure . }
        OPTIONAL { ?uri essglobal:economicSector ?secondaryActivities . }
}
ORDER BY DESC(UCASE(?name))
